Перем КонтекстЯдра;
Перем ПапкаФайловПлагина;
Перем V8Exchan; // Доработанная обработка универсального обмена данных из пакета Конвертации данных 2.1

// { Plugin interface
Функция ОписаниеПлагина(ВозможныеТипыПлагинов) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("Тип", ВозможныеТипыПлагинов.Утилита);
	Результат.Вставить("Идентификатор", Метаданные().Имя);
	Результат.Вставить("Представление", Метаданные().Синоним);
	
	Возврат Новый ФиксированнаяСтруктура(Результат);
КонецФункции

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
КонецПроцедуры

// Загружает данные из файла КД2 по переданному имени файла
//
// Параметры:
//  ИмяФайла - Строка - полное имя файла
//
Процедура ЗагрузитьИзФайла(ИмяФайла) Экспорт
	
	ИнициализироватьV8Exchan();
	
	V8Exchan.ИмяФайлаОбмена = ИмяФайла;
	V8Exchan.РежимОбмена = "Загрузка";
	V8Exchan.НеВыводитьНикакихИнформационныхСообщенийПользователю = Истина;
	V8Exchan.ВыполнитьЗагрузку();
	
КонецПроцедуры

// Загружает данные из файла КД2 по переданному текстовому документу
//
// Параметры:
//  ИмяФайла - ТекстовыйДокумент - содержимое файла конвертации
//
Процедура ЗагрузитьИзМакета(ТекстДок) Экспорт
	
	ИмяФайла = ПолучитьИмяВременногоФайла("xml");
	ТекстДок.Записать(ИмяФайла);
	
	Попытка
		ЗагрузитьИзФайла(ИмяФайла);
	Исключение
		УдалитьФайлы(ИмяФайла);
		ВызватьИсключение;
	КонецПопытки;
	
	УдалитьФайлы(ИмяФайла);
КонецПроцедуры
// } Plugin interface

Процедура ИнициализироватьV8Exchan()
	Если V8Exchan = Неопределено Тогда
		Файл = Новый Файл(ИспользуемоеИмяФайла);
		ИмяПлагина = ЭтотОбъект.Метаданные().Имя;
		ПапкаФайловПлагина = Файл.Путь + ИмяПлагина + "\";
		ПолноеИмяV8Exchan = ПапкаФайловПлагина + "Sulfur.V8Exchan.epf";
		
		V8Exchan = ВнешниеОбработки.Создать(ПолноеИмяV8Exchan, Ложь);
	КонецЕсли;
КонецПроцедуры
